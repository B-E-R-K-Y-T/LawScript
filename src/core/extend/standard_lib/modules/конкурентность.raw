ВКЛЮЧИТЬ стандартная_библиотека.структуры.примитивные_структуры
ВКЛЮЧИТЬ стандартная_библиотека._.util


ОПРЕДЕЛИТЬ ПРОЦЕДУРУ ждать_всех(задачи) (
    ДОКУМЕНТАЦИЯ (
        Передайте сюда массив запущенных в фоне процедур.
        ПРОБЕЛ
        Данная процедура будет ждать все фоновые задачи
        и вернет их результаты массивом, в том же порядке,
        что и во входном массиве.
    )

    ЗАДАТЬ длина = длина_массива(задачи);
    ЗАДАТЬ результаты = массив();
    ЗАДАТЬ результат = ПУСТОТА;

    ЦИКЛ индекс ОТ 0 ДО длина - 1 (
        результат = ЖДАТЬ достать_из_массива(задачи, индекс);
        добавить_в_массив(результаты, результат);
    )

    ВЕРНУТЬ результаты;
)

ОПРЕДЕЛИТЬ КЛАСС СобытиеСинхронизатор (
    ОПРЕДЕЛИТЬ КОНСТРУКТОР (ссылка) () (
        ссылка:_событие_случилось = ЛОЖЬ;
    )

    ОПРЕДЕЛИТЬ МЕТОД (ссылка) иницировать_событие() (
        БЛОКИРОВАТЬ (
            ссылка:_событие_случилось = ИСТИНА;
        )
    )

    ОПРЕДЕЛИТЬ МЕТОД (ссылка) выполнено() (
        БЛОКИРОВАТЬ (
            ВЕРНУТЬ ссылка:_событие_случилось;
        )
    )
)
