ВКЛЮЧИТЬ стандартная_библиотека.структуры.примитивные_структуры
ВКЛЮЧИТЬ стандартная_библиотека._.util


ОПРЕДЕЛИТЬ КЛАСС Список (
    ОПРЕДЕЛИТЬ КОНСТРУКТОР (ссылка)(мас=массив()) (
        ссылка:_массив = мас;
    )

    ОПРЕДЕЛИТЬ МЕТОД (ссылка) длина() (
        ВЕРНУТЬ длина_массива(ссылка:_массив);
    )

    ОПРЕДЕЛИТЬ МЕТОД (ссылка) сортировать() (
        ВЕРНУТЬ сортировать_массив(ссылка:_массив);
    )

    ОПРЕДЕЛИТЬ МЕТОД (ссылка) достать(индекс=0) (
        ВЕРНУТЬ достать_из_массива(ссылка:_массив, индекс);
    )

    ОПРЕДЕЛИТЬ МЕТОД (ссылка) удалить(индекс) (
        ВЕРНУТЬ удалить_из_массива(ссылка:_массив, индекс);
    )

    ОПРЕДЕЛИТЬ МЕТОД (ссылка) добавить(элемент) (
        ВЕРНУТЬ добавить_в_массив(ссылка:_массив, элемент);
    )

    ОПРЕДЕЛИТЬ МЕТОД (ссылка) в_массив(как_копия=ИСТИНА) (
        ЕСЛИ как_копия ТО (
            ВЕРНУТЬ _поверхностное_копирование(ссылка:_массив);
        )

        ВЕРНУТЬ ссылка:_массив;
    )
)


ОПРЕДЕЛИТЬ КЛАСС Очередь (
    ОПРЕДЕЛИТЬ КОНСТРУКТОР (ссылка)(размер) (
        ссылка:_максимум = размер;
        ссылка:_массив = массив();
    )

    ОПРЕДЕЛИТЬ МЕТОД (ссылка) длина() (
        БЛОКИРОВАТЬ (
            ВЕРНУТЬ длина_массива(ссылка:_массив);
        )
    )

    ОПРЕДЕЛИТЬ МЕТОД (ссылка) переполнена() (
        БЛОКИРОВАТЬ (
            ВЕРНУТЬ длина_массива(ссылка:_массив) БОЛЬШЕ ссылка:_максимум - 1;
        )
    )

    ОПРЕДЕЛИТЬ МЕТОД (ссылка) пуста() (
        ВЕРНУТЬ ссылка:длина() РАВНО 0;
    )

    ОПРЕДЕЛИТЬ МЕТОД (ссылка) взять() (
        БЛОКИРОВАТЬ (
            ЕСЛИ ссылка:пуста() ТО (
                ВЕРНУТЬ ЛОЖЬ;
            )

            ЗАДАТЬ элемент = достать_из_массива(ссылка:_массив, 0);
            удалить_из_массива(ссылка:_массив, 0);

            ВЕРНУТЬ элемент;
        )
    )

    ОПРЕДЕЛИТЬ МЕТОД (ссылка) положить(элемент) (
        БЛОКИРОВАТЬ (
            ЕСЛИ ссылка:переполнена() ТО (
                ВЕРНУТЬ ЛОЖЬ;
            )

            добавить_в_массив(ссылка:_массив, элемент);

            ВЕРНУТЬ ИСТИНА;
        )
    )
)
