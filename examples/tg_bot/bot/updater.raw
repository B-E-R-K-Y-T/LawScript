ОПРЕДЕЛИТЬ КЛАСС Актуализатор (
    ОПРЕДЕЛИТЬ КОНСТРУКТОР (ссылка) (базовый_адрес, токен="ВАШ_ТОКЕН", интервал_опроса=1) (
        ссылка:_токен = токен;
        ссылка:_базовый_адрес = базовый_адрес;
        ссылка:_интервал_опроса = интервал_опроса;
    )

    ОПРЕДЕЛИТЬ МЕТОД (ссылка) ожидание_новых_событий(очередь_событий) (
        ЗАДАТЬ сдвиг = 0;
        ЗАДАТЬ обновления;
        ЗАДАТЬ номер_обновления;
        ЗАДАТЬ события;
        ЗАДАТЬ событие;

        ПОКА ИСТИНА (
            обновления = ссылка:получить_событие(сдвиг);

            ЕСЛИ НЕ извлечь_из_таблицы(обновления, "ok") ТО (
                НАПЕЧАТАТЬ форматировать_строку("Произошла ошибка: {}", извлечь_из_таблицы(обновления, "description"));
                ПРОПУСТИТЬ;
            )

            события = извлечь_из_таблицы(обновления, "result");

            НАПЕЧАТАТЬ "получены события";

            ЦИКЛ счет ОТ 0 ДО длина_массива(события) - 1 (
                событие = достать_из_массива(события, счет);
                сдвиг = извлечь_из_таблицы(событие, "update_id") + 1;

                НАПЕЧАТАТЬ форматировать_строку("Обработка события со сдвигом: {}", сдвиг);

                ЕСЛИ есть_ключ_в_таблице(событие, "message") ТО (
                    ЗАДАТЬ сообщение = извлечь_из_таблицы(событие, "message");

                    ПОКА ИСТИНА (
                        КОНТЕКСТ (
                            очередь_событий:положить(сообщение);
                            ПРЕРВАТЬ;
                        )
                        ОБРАБОТЧИК ОчередьПолна КАК _ (
                            НАПЕЧАТАТЬ форматировать_строку("Очередь на обработку полна. Повторяю попытку запланировать событие: {}", сдвиг);
                            ЖДАТЬ В ФОНЕ асинхронный_сон(0.5);
                        )
                    )
                )
            )

            ЖДАТЬ В ФОНЕ асинхронный_сон(ссылка:_интервал_опроса);
        )
    )

    ОПРЕДЕЛИТЬ МЕТОД (ссылка) получить_событие(сдвиг=ПУСТОТА) (
        ЗАДАТЬ адрес = форматировать_строку("{}/getUpdates?timeout=30", ссылка:_базовый_адрес);

        ЕСЛИ сдвиг НЕРАВНО ПУСТОТА ТО (
            адрес = форматировать_строку("{}&offset={}", адрес, сдвиг);
        )

        ЗАДАТЬ ответ;

        ПОКА ИСТИНА (
            КОНТЕКСТ (
                ответ = запрос_в_интернет("GET", адрес, таблица());
                ПРЕРВАТЬ;
            )
            ОБРАБОТЧИК ОшибкаПротоколаПередачиГиперТекста КАК _ (
                НАПЕЧАТАТЬ "Произошла ошибка, при попытке обновить данные. Инициализация следующей попытки...";
                ЖДАТЬ В ФОНЕ асинхронный_сон(1);
            )
        )

        ВЕРНУТЬ извлечь_из_таблицы(ответ, "json");
    )
)
